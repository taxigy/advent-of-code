# [Day 6: Memory Reallocation]

A debugger program here is having an issue: it is trying to repair a memory reallocation routine, but it keeps getting stuck in an infinite loop.

In this area, there are sixteen memory banks; each memory bank can hold any number of blocks. The goal of the reallocation routine is to balance the blocks between the memory banks.

The reallocation routine operates in cycles. In each cycle, it finds the memory bank with the most blocks (ties won by the lowest-numbered memory bank) and redistributes those blocks among the banks. To do this, it removes all of the blocks from the selected bank, then moves to the next (by index) memory bank and inserts one of the blocks. It continues doing this until it runs out of blocks; if it reaches the last memory bank, it wraps around to the first one.

The debugger would like to know how many redistributions can be done before a blocks-in-banks configuration is produced that has been seen before.

For example, imagine a scenario with only four memory banks:

- The banks start with 0, 2, 7, and 0 blocks. The third bank has the most blocks, so it is chosen for redistribution.
- Starting with the next bank (the fourth bank) and then continuing to the first bank, the second bank, and so on, the 7 blocks are spread out over the memory banks. The fourth, first, and second banks get two blocks each, and the third bank gets one back. The final result looks like this: `2 4 1 2`.
- Next, the second bank is chosen because it contains the most blocks (four). Because there are four memory banks, each gets one block. The result is: `3 1 2 3`.
- Now, there is a tie between the first and fourth memory banks, both of which have three blocks. The first bank wins the tie, and its three blocks are distributed evenly over the other three banks, leaving it with none: `0 2 3 4`.
- The fourth bank is chosen, and its four blocks are distributed such that each of the four banks receives one: `1 3 4 1`.
- The third bank is chosen, and the same thing happens: `2 4 1 2`.

At this point, we've reached a state we've seen before: `2 4 1 2` was already seen. The infinite loop is detected after the fifth block redistribution cycle, and so the answer in this example is 5.

Given the initial block counts in your puzzle input, how many redistribution cycles must be completed before a configuration is produced that has been seen before?

### Solved

We can use a set to store all the combinations; then use that set as a function to check if such a combination has already occurred.

```clojure
(defn first-index-of-largest [input]
  (loop [s input
         i 0
         c (first s)
         m 0]
    (if (empty? s)
      m
      (recur (rest s)
             (inc i)
             (if (< c (first s))
               (first s)
               c)
             (if (< c (first s))
               i
               m)))))

(defn redistribute [from all]
  (let [l (count all)
        vall (vec all)
        value (get vall from)
        t (concat (repeat (inc from) 0) (repeat value 1) (repeat l 0))
        p (partition l t)
        r (apply map + p)
        s (map + r (assoc vall from 0))]
    s))

(defn calculate [input]
  (loop [all input
         done #{}
         steps 0]
    (if (contains? done all)
      steps
      (let [i (first-index-of-largest all)
            u (redistribute i all)]
        (recur u
               (conj done all)
               (inc steps))))))
```

Here, `first-index-of-largest` looks up the whole sequence and returns the first index of largest elements, no matter how many duplicates are present. I personally feel like this code is like 5x taller and 4 times heavier in the number of instructions compared to what it could be, but it's nice that it runs in linear time.

## Part 2

Out of curiosity, the debugger would also like to know the size of the loop: starting from a state that has already been seen, how many block redistribution cycles must be performed before that same state is seen again?

In the example above, 2 4 1 2 is seen again after four cycles, and so the answer in that example would be 4.

How many cycles are in the infinite loop that arises from the configuration in your puzzle input?

### Solved

This time it's easier with a map: keys are combinations, values are times a combination occurred. A map can also be used as a function to get the value by key.

We need to run the loop until the moment a combination occurs for the third time. Also, we need to maintain the number of steps done _after_ the target combination has occurred the first time. Counting the number of steps _before_ isn't actually necessary, but it's present in the code to maintain its connection with Part 1.

```clojure
(defn calculate [input]
  (loop [all input
         done {}
         steps-before 0
         steps-after 0]
    (if (= (done all) 3)
      steps-after
      (let [i (first-index-of-largest all)
            u (redistribute i all)
            t (done u 0)]
        (recur u
               (assoc done u (inc t))
               (if (= t 0) (inc steps-before) steps-before)
               (if (= t 1) (inc steps-after) steps-after))))))
```
