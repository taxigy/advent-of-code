# [Day 9: Stream Processing](http://adventofcode.com/2017/day/9)

A large stream blocks your path. According to the locals, it's not safe to cross the stream at the moment because it's
full of garbage. You look down at the stream; rather than water, you discover that it's a stream of characters.

You sit for a while and record part of the stream (your puzzle input). The characters represent groups - sequences that
begin with { and end with }. Within a group, there are zero or more other things, separated by commas: either another
group or garbage. Since groups can contain other groups, a } only closes the most-recently-opened unclosed group - that
is, they are nestable. Your puzzle input represents a single, large group which itself contains many smaller ones.

Sometimes, instead of a group, you will find garbage. Garbage begins with < and ends with >. Between those angle
brackets, almost any character can appear, including { and }. Within garbage, < has no special meaning.

In a futile attempt to clean up the garbage, some program has canceled some of the characters within it using !: inside
garbage, any character that comes after ! should be ignored, including <, >, and even another !.

You don't see any characters that deviate from these rules. Outside garbage, you only find well-formed groups, and
garbage always terminates according to the rules above.

Here are some self-contained pieces of garbage:

- `<>`, empty garbage.
- `<random characters>`, garbage containing random characters.
- `<<<<>`, because the extra < are ignored.
- `<{!>}>`, because the first > is canceled.
- `<!!>`, because the second ! is canceled, allowing the > to terminate the garbage.
- `<!!!>>`, because the second ! and the first > are canceled.
- `<{o"i!a,<{i<a>`, which ends at the first >.

...

Your goal is to find the total score for all groups in your input. Each group is assigned a score which is one more than
the score of the group that immediately contains it. (The outermost group gets a score of 1.)

- `{}`, score of 1.
- `{{{}}}`, score of 1 + 2 + 3 = 6.
- `{{},{}}`, score of 1 + 2 + 2 = 5.
- `{{{},{},{{}}}}`, score of 1 + 2 + 3 + 3 + 3 + 4 = 16.
- `{<a>,<a>,<a>,<a>}`, score of 1.
- `{{<ab>},{<ab>},{<ab>},{<ab>}}`, score of 1 + 2 + 2 + 2 + 2 = 9.
- `{{<!!>},{<!!>},{<!!>},{<!!>}}`, score of 1 + 2 + 2 + 2 + 2 = 9.
- `{{<a!>},{<a!>},{<a!>},{<ab>}}`, score of 1 + 2 = 3.

What is the total score for all groups in your input?

### Solved

So we have input like this:

```
{{<a!>},{<a!>},{<a!>},{<ab>}}
```

First, let's remove all the characters that are terminated by "!". There are no extra conditions. Any pair of characters
where the first one is "!" must go. Let's [`replace`](http://clojuredocs.org/clojure.string/replace):

```clojure
(defn unterminate [s] (clojure.string/replace s #"!." ""))
```

Now, let's remove all the garbage. To do this, let's use a regex with lazy group, find all the angular brackets and
simply remove them:

```clojure
(defn ungarbage [s] (clojure.string/replace s #"<.*?>" ""))
```

Finally, there are commas left. We don't need them.

```clojure
(defn uncomma [s] (clojure.string/replace s #"," ""))
```

Now, it's getting interesting. We're going to hack this problem using language features.

The value we have so far looks like a sequence of opening and closing curly braces; let's replace them with square
brackets and treat the result as a Clojure data structure, a vector!

```clojure
(defn into-vec [s]
  (-> s
      (clojure.string/replace #"\{" "[")
      (clojure.string/replace #"\}" "]")
      read-string
      eval))
```

Now we have a vector where all the elements are vectors. This data structure resembles what the problem definition is
telling. Groups are nestable, vectors are nestable. The only problem is that Clojure doesn't provide any scoring
mechanic to determine a level of nesting for any particular element in the vector. Let's make a function for that!

```clojure
(defn scores
  ([v] (scores v 1 []))
  ([v l r] (if (empty? v)
             (if (= l 1)
               r
               l)
             (concat r
                     [l]
                     (map #(scores % (inc l) r) v)))))
```

This function yields a massive vector which has vectors, and the result is very similar to source structure, with the
only difference: now we have all the scores.

Now all we need is to flatten the structure and `(apply +)` it.

For this sequence of steps, we can use thread macro. The only exception is the last step: can't simply `(apply +)` in a
thread-first macro. So let's make a little utility function for that:

```clojure
(defn sum [v]
  (apply + v))
```

Done.

The calculation then looks like this:

```clojure
(defn calculate [input]
  (-> input
      unterminate
      ungarbage
      uncomma
      into-vec
      scores
      flatten
      sum))
```

## Part 2

Now, you're ready to remove the garbage.

To prove you've removed it, you need to count all of the characters within the garbage. The leading and trailing < and >
don't count, nor do any canceled characters or the ! doing the canceling.

- `<>`, 0 characters.
- `<random characters>`, 17 characters.
- `<<<<>`, 3 characters.
- `<{!>}>`, 2 characters.
- `<!!>`, 0 characters.
- `<!!!>>`, 0 characters.
- `<{o"i!a,<{i<a>`, 10 characters.

How many non-canceled characters are within the garbage in your puzzle input?

### Solved

Here, we don't need to parse the input at all. All we need is a regular expression and a few manipulations on the seq:

```clojure
(defn calculate [input]
  (->> input
       unterminate
       (re-seq #"<.*?>")
       (map #(->> %
                  (drop 1)
                  (drop-last 1)))
       (map count)
       (apply +)))
```

Just take the input as string, only take all groups that are enclosed into angular brackets. Then drop the first
character and the last character ("<" and ">"). Get [`count`](http://clojuredocs.org/clojure.core/count) for every
string in seq. Then add them together.
