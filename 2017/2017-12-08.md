# [Day 8: I Heard You Like Registers](http://adventofcode.com/2017/day/8)

## Part 1

You receive a signal directly from the CPU. Because of your recent assistance with jump instructions, it would like you to compute the result of a series of unusual register instructions.

Each instruction consists of several parts: the register to modify, whether to increase or decrease that register's value, the amount by which to increase or decrease it, and a condition. If the condition fails, skip the instruction without modifying the register. The registers all start at 0. The instructions look like this:

```
b inc 5 if a > 1
a inc 1 if b < 5
c dec -10 if a >= 1
c inc -20 if c == 10
```

These instructions would be processed as follows:

- Because a starts at 0, it is not greater than 1, and so b is not modified.
- a is increased by 1 (to 1) because b is less than 5 (it is 0).
- c is decreased by -10 (to 10) because a is now greater than or equal to 1 (it is 1).
- c is increased by -20 (to -10) because c is equal to 10.

After this process, the largest value in any register is 1.

You might also encounter <= (less than or equal to) or != (not equal to). However, the CPU doesn't have the bandwidth to tell you what all the registers are named, and leaves that to you to determine.

What is the largest value in any register after completing the instructions in your puzzle input?

### Solved

We can clearly see that possible comparators are "equal", "not equal", "less than", "less than or equal", "greater than", "greater than or equal". No other comparators possible. The comparator is always present is any of the instructions.

Let's make a map of them:

```clojure
(def operators {">" >
                ">=" >=
                "==" =
                "!=" not=
                "<" <
                "<=" <=})
```

Same with operations, each operation can only be either inc or dec, no other possible options, and the operation is always present in any instruction, too.

```clojure
(def operations {"inc" +
                 "dec" -})
```

Now goes the most interesting part. Let's think of an instruction as of a seq of strings, like

```
'("b" "inc" "5" "if" "a" ">" "1")
```

To apply an instruction, we need to extract a few things from it:

- target register that will or will not be updated,
- target operator that will be applied to the valud of target register and addend,
- addend ("5" in the example above),
- constant "if" that we can neglect,
- compared register that is used to determine if target register has to be updated,
- comparator,
- and the operand that compared register's value is compared to.

We need to make sure all numbers are actually numbers and not strings (see [`read-string`](http://clojuredocs.org/clojure.core/read-string)), and we need to make sure that whenever a register whose value we don't know is peeked, it has to be 0.

The rest is easy, although quite verbose:

```clojure
(defn update-registers [registers instruction]
  (let [[target-register target-operation target-operand _ condition-register condition-operator condition-compare-to-value] instruction
        target-register-value (or (registers target-register) 0)
        compared-register-value (or (registers condition-register) 0)
        comparator (operators condition-operator)
        compared-value (read-string condition-compare-to-value)
        operation (operations target-operation)
        addend (read-string target-operand)]
    (if (comparator compared-register-value compared-value)
      (assoc registers target-register (operation target-register-value addend))
      registers)))
```

Finally, to calculate the max value of all registers, first run `update-registers` against every instruction, and use the result:

```clojure
(defn calculate [input]
  (let [result (reduce #(update-registers %1 %2) {} input)
        max-value (apply max (map val result))]
    max-value))
```

A few notes here:

- there has to be something better looking than `(apply max (map val â€¢))`, something similar to [`max-key`](http://clojuredocs.org/clojure.core/max-key).
- there have to be words that suit better than these long descriptions of all the values in `update-registers`'s "let" expression.

## Part 2

To be safe, the CPU also needs to know the highest value held in any register during this process so that it can decide how much memory to allocate to these operations. For example, in the above instructions, the highest value ever held was 10 (in register c after the third instruction was evaluated).

### Solved

Because we need both current and max values for every register, why not make the value of each register a map? Give it two keys, `:current` and `:max`, and it's enough.

```clojure
(defn safe0 [k x] (if (nil? (k x)) 0 (k x)))

(defn update-registers [registers instruction]
  (let [[target-register target-operation target-operand _ condition-register condition-operator condition-compare-to-value] instruction
        target (registers target-register)
        compared (registers condition-register)
        target-current (safe0 :current target)
        target-max (safe0 :max target)
        comparator (operators condition-operator)
        compared-current (safe0 :current compared)
        compared-value (read-string condition-compare-to-value)
        addend (read-string target-operand)
        target-upd ((operations target-operation) target-current addend)]
    (if (comparator compared-current compared-value)
      (assoc registers target-register {:current target-upd
                                        :max (max target-upd target-max)})
      registers)))

(defn calculate [input]
  (let [result (reduce #(update-registers %1 %2) {} input)
        max-value (apply max (map :max (vals result)))]
    max-value))
```

Here, `safe0` is a simple (and hardly reusable) utility function that takes a key and a map and returns either the value or 0 if the value is nil.

You may also see that the calculation of max value has changed. There's additional `(map :vals)` there to get the right values and skip current values of each register.

The `update-registers` function actually smells a bit. It'd totally be possible to create a few universal, reusable functions instead, and then make a domain-specific, data-specific function that would take just a couple lines. As usual, TBD.
