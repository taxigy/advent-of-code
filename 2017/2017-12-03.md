# [Day 3: Spiral Memory](http://adventofcode.com/2017/day/3)

## Part 1

The day 3, part 1 description:

You come across an experimental new kind of memory stored on an infinite two-dimensional grid.

Each square on the grid is allocated in a spiral pattern starting at a location marked 1 and then counting up while spiraling outward. For example, the first few squares are allocated like this:

```
17  16  15  14  13
18   5   4   3  12
19   6   1   2  11
20   7   8   9  10
21  22  23---> ...
```

While this is very space-efficient (no squares are skipped), requested data must be carried back to square 1 (the location of the only access port for this memory system) by programs that can only move up, down, left, or right. They always take the shortest path: the Manhattan Distance between the location of the data and square 1.

For example:

- Data from square 1 is carried 0 steps, since it's at the access port.
- Data from square 12 is carried 3 steps, such as: down, left, left.
- Data from square 23 is carried only 2 steps: up twice.
- Data from square 1024 must be carried 31 steps.

How many steps are required to carry the data from the square identified in your puzzle input all the way to the access port?

### Solved

The solution of this problem is purely arithmetic. There's no need to construct a spiral two-dimensional data structure or anyhow maintain the list. It takes just a few operations:

- find the nearest square root that is uneven, the _base_,
- "transpose" the fragment of an imaginary spiral at most 3 times, such that it's closest to the _base_ but not less than it,
- find the _vertical center_,
- the answer is the number of steps from "transposed" number and _vertical center_ plus canonical number of steps for the _base_.

Like this:

```clojure
(defn base [x]
  (int (Math/floor (Math/sqrt x))))

(defn find-closest-base [x]
  (let [sqrt-x (base x)
        biggest-base (if (odd? sqrt-x) sqrt-x (dec sqrt-x))]
    biggest-base))

(defn find-least-transposed-number [b x]
  (if (> (- x b) (* b b))
    (find-least-transposed-number b (- x b 1))
    x))

(defn get-canonical-length [b]
  (/ (- b 1) 2))

(defn find-spiral-vertical-center [b]
  (+ (* b b)
     (/ (+ b 1) 2)))

(defn get-vertical-steps [vertical-center least-transposed-x]
  (Math/abs (- vertical-center least-transposed-x)))

(defn calculate [x]
  (let [b (find-closest-base x)
        least-transposed-x (find-least-transposed-number b x)
        vertical-center (find-spiral-vertical-center b)
        vertical-steps (get-vertical-steps vertical-center least-transposed-x)
        horizontal-steps (inc (get-canonical-length b))]
    (+ vertical-steps horizontal-steps)))
```

## Part 2

As a stress test on the system, the programs here clear the grid and then store the value 1 in square 1. Then, in the same allocation order as shown above, they store the sum of the values in all adjacent squares, including diagonals.

So, the first few squares' values are chosen as follows:

- Square 1 starts with the value 1.
- Square 2 has only one adjacent filled square (with value 1), so it also stores 1.
- Square 3 has both of the above squares as neighbors and stores the sum of their values, 2.
- Square 4 has all three of the aforementioned squares as neighbors and stores the sum of their values, 4.
- Square 5 only has the first and fourth squares as neighbors, so it gets the value 5.

Once a square is written, its value does not change. Therefore, the first few squares would receive the following values:

```
147  142  133  122   59
304    5    4    2   57
330   10    1    1   54
351   11   23   25   26
362  747  806--->   ...
```

What is the first value written that is larger than your puzzle input?

### Solved

Solution for this cannot be arithmetic-only (?), so the calculation of a spiral is required.

First, let's begin with a few utility tools. The spiral goes right, then up, then left, then down. Define turns for each direction:

```clojure
(def turns {:right :up
            :up :left
            :left :down
            :down :right})
```

Let's assume initial square as index [0, 0], the X-axis goes to the right, and the Y-axis goes downwards (think computer screen addressing). The next position as [i, j] then can be calculated like this:

```clojure
(defn get-next-position [direction [i j]]
  (cond
    (= direction :right) [i (inc j)]
    (= direction :up) [(dec i) j]
    (= direction :left) [i (dec j)]
    (= direction :down) [(inc i) j]))
```

Spiral goes like this:

```
right
up
left — inc by 1
left
down
down
right — inc by 1
right
right
up
up
up
left — inc by 1
left
left
left
```

So let's assume that after every up and down turn we increase the number of steps:

```clojure
(defn get-steps-after-turn [direction steps-initial]
  (cond
    (= direction :up) (inc steps-initial)
    (= direction :down) (inc steps-initial)
    :otherwise steps-initial))
```

Now let's determine the next move. For it to happen, we need initial number of steps taken in current direction, the direction itself, the number of steps left to take in this direction, and current position expressed as [i, j].

The function then checks if the number of steps remaining to take in current direction is 0. If it is, it's time to turn. When we turn, we get the number of steps after turn (which depends on the next direction, see above).

Let's make the function operate on a map that has all the properties we need. For the convenience, this function may then return a new map of similar structure:

```clojure
(defn get-move [{direction :direction
                 steps-initial :steps-initial
                 steps-left :steps-left
                 position :position}]
  (cond
    (= steps-left 0) {:direction (turns direction)
                      :steps-initial (get-steps-after-turn direction steps-initial)
                      :steps-left (get-steps-after-turn direction steps-initial)
                      :position (get-next-position (turns direction) position)}
    :otherwise {:direction direction
                :steps-initial steps-initial
                :steps-left (dec steps-left)
                :position (get-next-position direction position)}))
```

Time to calculate a total sum of values in adjacent squares. Let's think of our spiral as a map where keys are [i, j] and values are totals. Like this:

```clojure
{[0 0] 1
 [0 1] 1
 [-1 1] 2
 [-1 0] 4} ;; and so on
```

So, for every [ix, jx], the value of that square is the total of squares with indices such that ix ∈ {i - 1, i, i + 1}, jx ∈ {j - 1, j, j + 1}.

I'm pretty sure there's a better implementation of a function that takes 1 vector and outpurs 9 vectors based on this formula, but here's mine:

```clojure
(defn square-indices [[i j]]
  (for [x [(dec i) i (inc i)]
        y [(dec j) j (inc j)]]
       (vector x y)))
```

Now, some values by the time of calculation can be nil. In Clojure, you can't add a number to a nil, so we need to treat nil as 0:

```clojure
(defn val0 [spiral [i j]]
  (let [e (spiral [i j])]
    (if (isa? e nil)
      0
      e)))
```

Calculation of the value of a square would then be very simple: take all the values as a seq, add them together. A very typical pattern for [`apply +`](https://clojuredocs.org/clojure.core/apply):

```clojure
(defn calculate-square [spiral [i j]]
  (apply + (map #(val0 spiral %) (square-indices [i j]))))
```

And so, we're ready to calculate our spiral. Let's make this function take 0 arguments and return an infinite lazy sequence. It will then be used in a [`loop`/`recur`](https://clojuredocs.org/clojure.core/loop) or in a [`take-while`](https://clojuredocs.org/clojure.core/take-while).

By the problem definition, we need to look for a number. So the function should return a sequence of numbers, that's it. No need to trace any other data.

And because it's lazy-seq, we can use it with `take-while`:

```clojure
(defn lazy-spiral
  ([] (lazy-spiral {[0 0] 1} {:direction :right :steps-initial 0 :steps-left 1 :position [0 0]}))
  ([spiral movement] (lazy-seq (cons (calculate-square spiral (:position movement))
                                     (lazy-spiral (assoc spiral (:position movement) (calculate-square spiral (:position movement)))
                                                  (get-move movement))))))
```

Now take-while this sequence to get the solution.
