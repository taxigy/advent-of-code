# [Day 24: Electromagnetic Moat](http://adventofcode.com/2017/day/24)

## Part 1

The CPU itself is a large, black building surrounded by a bottomless pit. Enormous metal tubes extend outward from the side of the building at regular intervals and descend down into the void. There's no way to cross, but you need to get inside.

No way, of course, other than building a bridge out of the magnetic components strewn about nearby.

Each component has two ports, one on each end. The ports come in all different types, and only matching types can be connected. You take an inventory of the components by their port types (your puzzle input). Each port is identified by the number of pins it uses; more pins mean a stronger connection for your bridge. A 3/7 component, for example, has a type-3 port on one side, and a type-7 port on the other.

Your side of the pit is metallic; a perfect surface to connect a magnetic, zero-pin port. Because of this, the first port you use must be of type 0. It doesn't matter what type of port you end with; your goal is just to make the bridge as strong as possible.

The strength of a bridge is the sum of the port types in each component. For example, if your bridge is made of components 0/3, 3/7, and 7/4, your bridge has a strength of 0+3 + 3+7 + 7+4 = 24.

For example, suppose you had the following components:

```
0/2
2/2
2/3
3/4
3/5
0/1
10/1
9/10
```

With them, you could make the following valid bridges:

- 0/1
- 0/1--10/1
- 0/1--10/1--9/10
- 0/2
- 0/2--2/3
- 0/2--2/3--3/4
- 0/2--2/3--3/5
- 0/2--2/2
- 0/2--2/2--2/3
- 0/2--2/2--2/3--3/4
- 0/2--2/2--2/3--3/5

(Note how, as shown by 10/1, order of ports within a component doesn't matter. However, you may only use each port on a component once.)

Of these bridges, the strongest one is 0/1--10/1--9/10; it has a strength of 0+1 + 1+10 + 10+9 = 31.

What is the strength of the strongest bridge you can make with the components you have available?

### Solved

Let's first parse input. It's a column of pairs of numbers separated by a slash. A typical case for [`re-seq`](https://clojuredocs.org/clojure.core/re-seq).

```clojure
(def input (map (partial mapv read-string)
                (map (partial drop 1)
                     (re-seq #"(\d+)\/(\d+)" (slurp "input.txt")))))
```

Now, let's define the algorithm:

1. Find an element that has 0 as one of the two items in the pair. This will be out square 1. Because there can be more than one such pair, it will be a collection.
2. Mapping through collection of possible _heads_, go through all possible _tails_ and pick the matching ones; a matching tail is the one that contains one value that is equal to the unused value of current _head_.
3. Go full on recursion.

There's a very convenient standard function in Clojure, [`group-by`](https://clojuredocs.org/clojure.core/group-by), that will allow us splitting the rest of current collection into two groups: possible next heads and the rest. This function returns a map with two keys, true and false; true has all the values that matched predicate, and false are the rest.

To make our life easier, we can reverse possible next heads when the matching element is second. It's just so that in all the further calculations, we'll use `second` as a connection point to the tails.

```clojure
(defn split [x all]
  (let [m (group-by (fn [[a b]] (or (= a x)
                                    (= b x)))
                    all)
        head (get m true)
        norm-head (mapv (fn [[a b]] (if (= x a)
                                      [a b]
                                      [b a]))
                        head)]
    {:head norm-head
     :tail (get m false)}))
```

It's time to determine all possible steps from all possible "square 1" heads.

To do this, we'll declare a function that takes input and calls itself with a few extra arguments:

- all strengths that we've found so far (beginning with an empty seq),
- current accumulated strength,
- and the input itself.

Let's accumulate the strength of the bridge per every next recursive call of our function. Strength is calculated as previous strength and sum of the pair added to it.

The last thing is not that obvious: we don't care about lengths of the bridges, and we don't need to maintain nesting in order to know what bridges we've found so far. We can easily put every result of current recursive call into a flat seq. And there's a function for that, [`mapcat`](https://clojuredocs.org/clojure.core/mapcat).

Our function that detects all possible bridges would then look like this:

```clojure
(defn possible-steps
  ([m] (possible-steps '() 0 (split 0 m)))
  ([r t m] (let [{:keys [head tail]} m]
             (if (empty? head)
               r
               (mapcat (fn [h] (possible-steps (cons (apply + t h) r)
                                               (apply + t h)
                                               (split (second h) (concat tail
                                                                         (filter #(not= h %) head)))))
                       head)))))
```

We get a flat seq of all strengths. It's now just a matter of getting max of all:

```clojure
(defn calculate [input]
  (apply max (possible-steps input)))
```

The returned result is the solution.

## Part 2

The bridge you've built isn't long enough; you can't jump the rest of the way.

In the example above, there are two longest bridges:

- 0/2--2/2--2/3--3/4
- 0/2--2/2--2/3--3/5

Of them, the one which uses the 3/5 component is stronger; its strength is 0+2 + 2+2 + 2+3 + 3+5 = 19.

What is the strength of the longest bridge you can make? If you can make multiple bridges of the longest length, pick the strongest one.

### Solved

Alright, in part 2, we need to maintain all the bridges as collections of strengths. The values in collections may remain the same as before and represent accumulated strength, where the first item in the collection is the smallest, and the last item in the collection is largest. But because we use `cons` and a seq, it will actually be reversed: first element will be the largest.

Now, because we can't rely on a flat collection any longer, let's replace `mapcat` with traditional `map`:

```clojure
(defn possible-steps
  ([m] (possible-steps '() 0 (split 0 m)))
  ([r t m] (let [{:keys [head tail]} m]
             (if (empty? head)
               r
               (map (fn [h] (possible-steps (cons (apply + t h) r)
                                            (apply + t h)
                                            (split (second h) (concat tail
                                                                      (filter #(not= h %) head)))))
                    head)))))
```

The result is a collection of collections where each collection may have collections. The nesting is very deep and cannot be easily predicted. The larger the input, the more layers potentially will be in the returned results.

But what we need is a collection of collections, just that! Well, let's make our own flattening function:

```clojure
(defn flat [coll]
  (mapcat #(if (every? coll? %)
             (flat %)
             (list %))
          coll))
```

It takes a collection and uses the power of `mapcat` to put the result of recursion into a flat collection, regardless of the number of calls. But to not flatten too much, we use `list`.

Now we have a collection where each item is a bridge. A bridge is represented by a collection, where the first element is the largest and defined strength of the bridge. The number of items, in turn, defined length of the bridge. We're close.

Let's take longest bridges from the result. To do that, we'll sort it by number of items, and then use [`take-while`](https://clojuredocs.org/clojure.core/take-while) to only take items that are of the same (maximum) length.

```clojure
(defn take-longest [coll]
  (let [c (sort #(compare (count %2) (count %1)) coll)
        longest (take-while #(= (count %) (count (first c))) c)]
    longest))
```

Done with longest. Now, of all the longest, let's find the strongest!

To do that, we simply need to find maximum first item of all bridges-collections. And filter all the longest bridges by that max item: whatever bridge is weaker, it's filtered out.

```clojure
(defn take-strongest [coll]
  (let [m (apply max (map first coll))
        strongest (filter #(= m (first %)) coll)]
    strongest))
```

Done. Now, let's adjust our calculation function:

1. Find all bridges recursively, ending up with a deeply nested collection.
2. Flatten it.
3. Find longest of all.
4. Find strongest of longest.

and because the challenge only asks for the strength of the longest and strongest bridge, take first element of the first bridge from step 4.

Let's go:

```clojure
(defn calculate [input]
  (let [steps (possible-steps input)
        fsteps (flat nested-result)
        longest (take-longest fsteps)
        strongest (take-strongest longest)]
    (first (first strongest))))
```

The result returned by our calculation is the solution.
