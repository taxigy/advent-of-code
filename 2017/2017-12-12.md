# [Day 12: Digital Plumber](http://adventofcode.com/2017/day/12)

Walking along the memory banks of the stream, you find a small village that is experiencing a little confusion: some programs can't communicate with each other.

Programs in this village communicate using a fixed system of pipes. Messages are passed between programs using these pipes, but most programs aren't connected to each other directly. Instead, programs pass messages between each other until the message reaches the intended recipient.

For some reason, though, some of these messages aren't ever reaching their intended recipient, and the programs suspect that some pipes are missing. They would like you to investigate.

You walk through the village and record the ID of each program and the IDs with which it can communicate directly (your puzzle input). Each program has one or more programs with which it can communicate, and these pipes are bidirectional; if 8 says it can communicate with 11, then 11 will say it can communicate with 8.

You need to figure out how many programs are in the group that contains program ID 0.

For example, suppose you go door-to-door like a travelling salesman and record the following list:

```
0 <-> 2
1 <-> 1
2 <-> 0, 3, 4
3 <-> 2, 4
4 <-> 2, 3, 6
5 <-> 6
6 <-> 4, 5
```

In this example, the following programs are in the group that contains program ID 0:

- Program 0 by definition.
- Program 2, directly connected to program 0.
- Program 3 via program 2.
- Program 4 via program 2.
- Program 5 via programs 6, then 4, then 2.
- Program 6 via programs 4, then 2.

Therefore, a total of 6 programs are in this group; all but program 1, which has a pipe that connects it to itself.

### Solved

As usual, let's parse input. The input is a column of numbers, where the first defines output and the rest define inputs. Let's match them and turn into numbers:

```clojure
(def input
  (->> "input.txt"
       slurp
       clojure.string/split-lines
       (map (partial re-seq #"\d+"))
       (map (partial map read-string))))
```

The algorithm is actually very simple: we need a set where values are programs that are reachable from 0, so let's go through the input and simply check each item if any of its elements are in the result set; if there is at least one, add them to the set; if there's none, skip.

A search function would then look like this:

```clojure
(defn search [result x]
  (if (some result x)
    (apply conj result x)
    result))
```

Note that we can do `conj` on a set, and it will add items to the set; because each item is a seq of numbers, we need to `apply conj` instead.

The calculation function itself is then a `reduce`.

When run against input, it doesn't return the right answer, because one run doesn't catch all the items that are reachable in more than one hop; to get items that are reachable in N hops, we need to cycle input N times. To not cycle too much, we can pessimistically determine the longest possible cycle from the longest item in the input. Good for us, it's not a big number, it's somewhat around 7.

The result would then look like:

```clojure
(defn calculate [input]
  (let [f (first input)
        result (into #{} f)
        max-len (apply max (map count input))
        rs (reduce search result (take (* max-len (count input))
                                       (cycle input)))]
    (count rs)))
```

## Part 2

There are more programs than just the ones in the group containing program ID 0. The rest of them have no way of reaching that group, and still might have no way of reaching each other.

A group is a collection of programs that can all communicate via pipes either directly or indirectly. The programs you identified just a moment ago are all part of the same group. Now, they would like you to determine the total number of groups.

In the example above, there were 2 groups: one consisting of programs 0,2,3,4,5,6, and the other consisting solely of program 1.

How many groups are there in total?

### Solved

To successfully collect all possible distinct sequences of programs, we need to maintain a collection of them. Let's do the same thing as in part 1 and collect every valid sequence as set. To maintain a various number of such sequences, let's use a map. It could as well be a vector or a seq though.

The algorithm is then as follows:

1. Begin with empty map as accumulated result.
2. For every item in the input collection, check if any of its items is present in any of the sets that are values of result map.
3. If there is an intersection, put union of the two under that same key into the result map.
4. If there are none, create a new key; turn input item from a seq into a set, and put it as a value.
5. If there are more than one vals in the result map that intersect with given input, union all of them together with the input, put under the first of all found keys; empty values under the rest of found keys.

Let's prepare a few utility functions to support it.

First, intersection:

```clojure
(defn intersect? [s c]
  (some s c))
```

It takes a set and a coll; because a set implements IFn, it can be used as the first argument of `some`.

Next, a union:

```clojure
(defn union [s & ss]
  (into #{} (apply clojure.set/union s ss)))
```

This function takes any number of sets and returns a union of them. It doesn't matter if input values are actually sets or seqs; the only thing that matters is to put the result into an empty set, because if one of the arguments was, say, a vector, the function would return a vector, and we don't need that.

Now, a function to empty the keys. An interesting one.

Because we're going to assoc a collection of various number of items with result map, we definitely need to use either `merge` or `apply assoc`; let's use the latter. To make it work then, our function that empties sets per collection of keys should simply return a flat seq to compy with `apply assoc`:

```clojure
(defn empty-keys [m ks]
  (let [m' (select-keys m ks)]
    (interleave (keys m') (map empty (vals m')))))
```

To make the accumulation of result work properly, we need to adjust our search function that is called per every item in the input because it's passed as the first argument to `reduce`. Just as before, it takes two arguments: accumulated result and current item.

Now, if there is an intersection somewhere in result with current item, it should update the result accordingly: union the first occurrence, empty the rest (safe in case of nil, too). If it doesn't find an intersection, it introduces a new key to the accumulated result map. Because it's a map, we can safely use `(count â€¢)` to get the size of map and use it as a key that is guaranteed to not be present in the map (remember that we don't remove items from the map!).

Our search function is a bit obscure and could definitely look better, but here it is:

```clojure
(defn search [result x]
  (let [found (filter #(some (second %) x) result)
        [fk fs] (first found)
        rf (rest found)]
    (if-not (empty? found)
      (apply assoc result
             fk (union fs (mapcat second rf) x)
             (empty-keys result (map first rf)))
      (assoc result (count result) (into #{} x)))))
```

The calculation is then turns to be a sequence of three steps:

1. Find all distinct sequences in the input.
2. Filter them such that we end up with only non-empty ones.
3. Return the number of items in the map.

So here it goes:

```clojure
(defn calculate [input]
  (let [max-len (apply max (map count input))
        rs (reduce search {} (take (* max-len (count input))
                                   (cycle input)))
        rs' (filter (comp not-empty second) rs)]
    (count rs')))
```

For 2000 lines of input, it takes a few seconds on a typical MacBook to run, which is super bad as the algo is extremely inefficient in terms of time-complexity. But again, since we're not restricted by execution time, and the input is short, we're cool with that.
