# [Day 16: Permutation Promenade](http://adventofcode.com/2017/day/16)

## Part 1

You come upon a very unusual sight; a group of programs here appear to be dancing.

There are sixteen programs in total, named a through p. They start by standing in a line: a stands in position 0, b stands in position 1, and so on until p, which stands in position 15.

The programs' dance consists of a sequence of dance moves:

- Spin, written sX, makes X programs move from the end to the front, but maintain their order otherwise. (For example, s3 on abcde produces cdeab).
- Exchange, written xA/B, makes the programs at positions A and B swap places.
- Partner, written pA/B, makes the programs named A and B swap places.

For example, with only five programs standing in a line (abcde), they could do the following dance:

- s1, a spin of size 1: eabcd.
- x3/4, swapping the last two programs: eabdc.
- pe/b, swapping programs e and b: baedc.

After finishing their dance, the programs end up in order baedc.

You watch the dance for a while and record their dance moves (your puzzle input). In what order are the programs standing after their dance?

### Solved

Spin, in this case, it right-rotation of a sequence, think right shift. To do this, we should drop n items from the initial sequence and add them to its head:

```clojure
(defn rotate-right [v n]
  (let [q (- (count v) n)]
    (vec (concat (drop q v)
                 (take q v)))))
```

Exchange and partner operations are element swap, where the former points at items by index and the latter requires lookup by value:

```clojure
(defn swap [v i j]
  (assoc v j (v i) i (v j)))

(defn swap-vals [v x y]
  (let [i (.indexOf v x)
        j (.indexOf v y)]
    (swap v i j)))
```

The input we have is a comma-separated one row of 10k operations. Let's split it:

```clojure
(def input
  (-> "input.txt"
      slurp
      (clojure.string/split #",")))
```

Now, we have operations like "s1", "x3/4" and "pe/b" as each item in `input` collection.

In case of "spin", we simply need to parse the number. In case of "exchange" and "partner", split the string by "/" character and parse values. And in all cases, the first character in the string, after we check it, doesn't matter.

Working with strings and collection in one function looks a bit ugly in Clojure, but here's the code:

```clojure
(defn parse-num [s]
  (->> s
       rest
       clojure.string/join
       read-string))

(defn parse-nums [s]
  (->> s
       rest
       clojure.string/join
       (re-seq #"\d+")
       (map read-string)))

(defn parse-vals [s]
  (->> s
       rest
       clojure.string/join
       (re-seq #"[a-p]")
       (map first)))
```

(could use [`re-seq`](https://clojuredocs.org/clojure.core/re-seq) though)

We have transformation functions per operation kind, now let's make a transformation function that takes input vector and the operation as a string, applies the latter, and returns the resulting vector:

```clojure
(defn transform [v op]
  (case (first op)
    \s (rotate-right v (parse-num op))
    \x (apply swap v (parse-nums op))
    \p (apply swap-vals v (parse-vals op))
    :default v))
```

So, the final calculation function would look like this:

```clojure
(defn calculate [input]
  (loop [ops input
         result (vec "abcdefghijklmnop")]
    (if (empty? ops)
      (clojure.string/join result)
      (recur (rest ops)
             (transform result (first ops))))))
```

## Part 2

Now that you're starting to get a feel for the dance moves, you turn your attention to the dance as a whole.

Keeping the positions they ended up in from their previous dance, the programs perform it again and again: including the first dance, a total of one billion (1000000000) times.

In the example above, their second dance would begin with the order baedc, and use the same dance moves:

- s1, a spin of size 1: cbaed.
- x3/4, swapping the last two programs: cbade.
- pe/b, swapping programs e and b: ceadb.

In what order are the programs standing after their billion dances?

### Solved

So, the `calculate` function now generates just one step, and there are a billion of them:

```clojure
(defn dance [input init]
  (loop [ops input
         result (vec init)]
    (if (empty? ops)
      (clojure.string/join result)
      (recur (rest ops)
             (transform result (first ops)))))))
```

Because our sequence isn't very long, it's likely it repeats within the sequence of 1 billion consecutive transformations. Let's find out when exactly; in other words, what is the _cycle_ in the sequence:

```clojure
(defn dances [input init]
  (lazy-seq (cons (dance input init)
                  (dances input (dance input init)))))

(defn detect-loop [coll]
  (loop [[x & xs] coll
         mem {}
         step 0]
    (if (mem x)
      {:cycle (- step (mem x))
       :offset (mem x)}
      (recur xs
             (assoc mem x step)
             (inc step)))))
```

Again, a lazy sequence that gives us unlimited number of consecutive transformations, and we only need a couple of them. The execution of `detect-loop` shouldn't take very long until it finds the second occurrence of a string that has already been found. And because a dance is pure, we can safely assume the same input string will produce the same output. It's therefore safe to claim the loop is always the same.

Now, knowing the cycle (as a number), we can easily remove the multiple of it from 1 billion using modulo operator. If the cycle is 50, for example, then the remainder will be 0. If it's 60, the remainder will be 40, so we would only need to perform 40 operations, which is way faster.

Because the lazy seq that `dances` returns has already the first transformation as the first element, we actually need to perform one transform less when we calculate the result.

Final calculation would look like this:

```clojure
(defn calculate [input]
  (let [ds (dances input "abcdefghijklmnop")
        {cyc :cycle off :offset} (detect-loop ds)
        rep (mod 1000000000 cyc)
        result (nth ds (+ off rep -1))]
    result))
```

And it takes less than a minute to return the right answer.
